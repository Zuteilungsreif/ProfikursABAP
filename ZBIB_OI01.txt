*&---------------------------------------------------------------------*
*& Report  ZBIB_OI01                                                   *
*&---------------------------------------------------------------------*

REPORT  zbib_OI01                  .

DATA pfile(100) TYPE c.
pfile =  'c:\test.xls'.

INCLUDE ole2incl.

DATA: excelobj TYPE ole2_object,
      excelwbs TYPE ole2_object,
      excelwb TYPE ole2_object,
      excelwss TYPE ole2_object,
      excelws TYPE ole2_object.


TABLES: zbibverlage.

DATA it_verlage LIKE zbibverlage OCCURS 0 WITH HEADER LINE.
DATA: zeile TYPE i.

SELECT * FROM zbibverlage INTO TABLE it_verlage.


CREATE OBJECT excelobj 'EXCEL.APPLICATION'.
PERFORM checkerror USING 'Create EXCEL.APPLICATION'.

SET PROPERTY OF excelobj  'Visible' = 1.

CALL METHOD OF excelobj 'Workbooks' = excelwbs.
PERFORM checkerror USING 'Get Workbooks'.

CALL METHOD OF excelwbs 'Add' = excelwb.
PERFORM checkerror USING 'Workbooks.Add'.

CALL METHOD OF excelwb 'Worksheets' = excelwss.
PERFORM checkerror USING 'Get Worksheets'.

CALL METHOD OF excelwss 'Add' = excelws.
PERFORM checkerror USING 'Add Worksheet'.

SET PROPERTY OF excelws  'Name' = 'Übersichtblatt'.

PERFORM zellefuellen USING 1 1 'VerlagsNr' excelws.
PERFORM zellefuellen USING 1 2 'Verlagsbezeichnung' excelws.
PERFORM zellefuellen USING 1 3 'Anzahl ausgeliehen Bücher' excelws.

LOOP AT it_verlage.
  zeile = sy-tabix + 1.
  PERFORM zellefuellen USING zeile 1 it_verlage-verlag excelws.
  PERFORM zellefuellen USING zeile 2 it_verlage-verlagbez excelws.
  PERFORM zellefuellen USING zeile 3 it_verlage-anzahl excelws.
ENDLOOP.

PERFORM zellefaerben USING 1 1 15 excelws.
PERFORM zellefaerben USING 1 2 15 excelws.
PERFORM zellefaerben USING 1 3 15 excelws.

DATA: formel(100) TYPE c,
      n_zeile(5) TYPE n.
MOVE zeile TO n_zeile.
CONCATENATE '=SUM(C2:C' n_zeile ')' INTO formel.

zeile = zeile + 1.

PERFORM zellefuellen USING zeile 2 'Summe' excelws.
PERFORM zellefuellen USING zeile 3 formel excelws.


CALL METHOD OF excelwb 'SaveAs'
  EXPORTING
    #1 = pfile.
PERFORM checkerror USING 'saveas'.

FREE OBJECT excelobj.

WRITE /  'Programm erfolgreich beendet'.

************************************************************************
FORM zellefuellen USING
  z TYPE i
  s TYPE i
  value
  worksheet TYPE  ole2_object.

  DATA:      excelrange TYPE ole2_object.

  CALL METHOD OF worksheet 'Cells' = excelrange
    EXPORTING
      #1 = z
      #2 = s.
  PERFORM checkerror USING 'Get Range'.

  SET PROPERTY OF excelrange 'Value' = value .
  PERFORM checkerror USING 'Range.Value = ..'.

ENDFORM.                    "ZELLEFUELLEN

************************************************************************

FORM zellefaerben USING
  z TYPE i
  s TYPE i
  farbe TYPE i
  worksheet TYPE  ole2_object.

  DATA:  excelrange TYPE ole2_object,
         excelinterior TYPE ole2_object.

  CALL METHOD OF worksheet 'Cells' = excelrange
    EXPORTING
      #1 = z
      #2 = s.
  PERFORM checkerror USING 'Get Range'.

  CALL METHOD OF excelrange 'Interior' = excelinterior.
  PERFORM checkerror USING 'Get Range.Interior'.

  SET PROPERTY OF excelinterior 'ColorIndex' = farbe.

ENDFORM.                    "ZELLEFAERBEN

************************************************************************
FORM checkerror USING text.
  IF sy-subrc <> 0.
    WRITE: / 'Fehler bei: ',text, sy-subrc.
    STOP.
  ENDIF.
ENDFORM.                    "CHECKERROR